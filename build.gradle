buildscript {
    ext {
        datastaxJavaDriverVersion = "4.17.0"
    }
}

plugins {
    id 'java'
    id "application"
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.github.node-gradle.node' version '7.0.2'
    id "org.sonarqube" version "5.1.0.4882"
}

allprojects {
    group = 'kr.hakdang.cassdio'
    version = '0.1.0'
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply {
        plugin "idea"
        plugin "java-library"
        plugin "org.springframework.boot"
        plugin "io.spring.dependency-management"
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        // Lombok
        compileOnly("org.projectlombok:lombok")
        annotationProcessor("org.projectlombok:lombok")

        testCompileOnly("org.projectlombok:lombok")
        testAnnotationProcessor("org.projectlombok:lombok")

        // Test
        testImplementation("org.springframework.boot:spring-boot-starter-test")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    tasks.register('unitTest', Test) {
        group = "verification"
        useJUnitPlatform {
            excludeTags 'integration-test'
        }
    }

    tasks.register('integrationTest', Test) {
        group = "verification"
        useJUnitPlatform {
            includeTags 'integration-test'
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        testLogging {
            showExceptions = true
            exceptionFormat = 'FULL'
            showCauses = true
            events = ['FAILED']
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "hakdang_cassdio"
        property "sonar.organization", "hakdang"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
